#!/bin/csh -f
set name = $1
shift
set basedir = $1
shift
set linkprefix = "$1"
shift
echo "making lib"$name.a
rm -f lib$name.a shr.o
set list = ()
foreach ename ($argv:q)
	if ("$ename:e" == "o") then 
		set list = ($ename $list)
	endif
end
echo "Creating exports list."
$basedir/etc/exp.csh $name.exp $basedir $list
if ($status != 0) then
	echo Could not create exports list for lib$name.a
	exit(-1)
endif	
echo "Creating shared object."
$linkprefix ld -bnoentry -T512 -H512 -bE:$name.exp -bhalt:4 -bM:SRE -o shr.o $*
if ($status != 0) then
	echo Could not create shared library: lib$name.a
	exit(-1)
endif
echo "Archiving."
if (-f shr.o) then
	ar clq lib$name.a shr.o
	if ($status != 0) then
		echo Could not create shared library: lib$name.a
		rm shr.o
		exit(-1)
	endif
	rm shr.o
else
	echo Could not create shared library: lib$name.a
	exit(-1)
endif
