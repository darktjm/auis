#!/bin/csh -f
# Copyright 1992 Andrew Consortium and Carnegie Mellon University
# All Rights reserved.
# $Disclaimer: 
# Permission to use, copy, modify, and distribute this software and its 
# documentation for any purpose and without fee is hereby granted, provided 
# that the above copyright notice appear in all copies and that both that 
# copyright notice and this permission notice appear in supporting 
# documentation, and that the name of IBM not be used in advertising or 
# publicity pertaining to distribution of the software without specific, 
# written prior permission. 
#                         
# THE COPYRIGHT HOLDERS DISCLAIM ALL WARRANTIES WITH REGARD 
# TO THIS SOFTWARE, INCLUDING ALL IMPLIED WARRANTIES OF 
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL ANY COPYRIGHT 
# HOLDER BE LIABLE FOR ANY SPECIAL, INDIRECT OR CONSEQUENTIAL 
# DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF USE, 
# DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE 
# OR OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION 
# WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
# 
#  $

# figures out a relative path from the current directory to TARGET,
# then uses it in an imake command.

if ($#argv>0) then
	set TARGET = $1
	if "$TARGET" == "---" then 
		set TARGET = `andrewdirs andrewdir`
	endif
	shift
else
	set TARGET = `andrewdirs andrewdir`
endif

if "$1" == "---" then
	shift
endif

if (! -e Imakefile) then
	echo "==>>> No Imakefile - Cannot Generate Makefile <<<=="
	exit(1)
endif
set DIR=`pwd | tr '/' ' '`
set TOP=`echo ${TARGET} | tr '/' ' '`
if ($#TOP > $#DIR) then
	set MAX=$#DIR
else
	set MAX=$#TOP
endif
set LIMIT=$#DIR
set REST = ${TARGET}
set COUNT=1
while (${COUNT} < ${MAX} && ("${TOP[${COUNT}]}" == "${DIR[${COUNT}]}"))
	@ COUNT++
	set REST = `echo ${REST} | sed -e 's,/[^/]*,,'`
end
set REL="."
while (${LIMIT} >= ${COUNT})
	set REL=${REL}/..
	@ LIMIT--
end
set REL = ${REL}${REST}

echo "##### Generating Makefile with Relative Path:"
echo "	${REL}"

imake -T imake.tmpl -I${REL} -I${REL}/config -s Makefile -DTOPDIR=${REL} $argv:q


